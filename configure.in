dnl
dnl  almepro -- allocation memory profiler library
dnl 
dnl  Copyright (c) 2002 Bonelli Nicola <bonelli@blackhats.it>
dnl			Banchi Leonardo <benkj@antifork.org>
dnl 
dnl  All rights reserved.
dnl

dnl Process this file with autoconf to produce a configure script.

AC_INIT([almepro],[1.x],[bonelli@blackhats.it])
AC_CONFIG_SRCDIR([include/amp.h])
AC_CONFIG_HEADER([include/config.h])

AC_SUBST(BSDSIG_DEP)
AC_SUBST(AR)
AC_SUBST(AS)
AC_SUBST(LD)

dnl Guess target
AC_CANONICAL_TARGET

case "$target_os" in 
*linux*)
	bsd_signal=no
	AC_SUBST(TARGET_OS, [linux])
	;;
*freebsd*)
	bsd_signal=no
	AC_SUBST(TARGET_OS, [freebsd])
	;;
*openbsd*)
	bsd_signal=yes
	AC_SUBST(TARGET_OS, [openbsd])
	;;
*)
	AC_MSG_ERROR([${target_os}: unsupported system])
	;;
esac

case "$target_cpu" in
*i*86*)
	AC_SUBST(TARGET_CPU, [i386])
	;;
*)
	AC_SUBST(TARGET_CPU, [$target_os])
	;;
esac

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_CHECK_PROGS(LD, ld, @true)
AC_PROG_CPP
AC_CHECK_PROGS(AS, as, @true)
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S
AC_PROG_RANLIB
AC_CHECK_PROGS(AR, ar, @true)

AC_SUBST(SED)
AC_SUBST(ENV)
AC_SUBST(GETOPT)
AC_SUBST(WHICH)
AC_PATH_PROGS(SED, sed)
AC_PATH_PROGS(ENV, env, @true)
AC_PATH_PROGS(GETOPT, getopt, @true)
AC_PATH_PROGS(WHICH, which)

if ! test -x "$SED" -a -x "$ENV" -a -x "$GETOPT" -a -x "$WHICH"; then 
	AC_MSG_ERROR([almepro need the following program: sed, env, getopt])
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h unistd.h)
AC_CHECK_HEADERS([bfd.h],, [AC_MSG_ERROR([almepro needs bfd-library from gnu binutils])])

AH_TEMPLATE(VERSION, [almepro version])
AC_DEFINE_UNQUOTED(VERSION, "`cat VERSION`")

AH_TEMPLATE([HAVE_BSD_SYS_QUEUE_H],
[define if the sys/queue.h BSD header is present ])
AC_CHECK_HEADERS(sys/queue.h, [AC_EGREP_CPP(have_bsd_sys_queue, [
#include <sys/queue.h>
#ifdef SLIST_HEAD
  have_bsd_sys_queue
#endif
], AC_DEFINE([HAVE_BSD_SYS_QUEUE_H]))])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strdup strerror strstr asprintf vasprintf __libc_malloc __progname )
AC_REPLACE_FUNCS(strlcpy strlcat)

dnl Checks for libraries.
AC_CHECK_LIB([dl], [dlopen])
AC_CHECK_LIB([bfd], [bfd_init])
AC_CHECK_LIB([iberty], [cplus_demangle])
AC_CHECK_LIB([intl], [gettext])

if test "$bsd_signal" = "yes"; then
	BSDSIG_DEP=bsd_signal/libbsd_signal.a
	LIBS="$LIBS -lbsd_signal"
	LDFLAGS="$LDFLAGS -Lbsd_signal"
fi


AC_CONFIG_FILES([src/Makefile src/bsd_signal/Makefile script/Makefile script/runamp example/Makefile])

AC_OUTPUT
